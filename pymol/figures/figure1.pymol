# Commands to generate panels of figure 1
# This figure is currently using AR as an
# example as there are two cartoonishly conserved
# hydrogen bonds (and a pi-pi but we're not currently
# showing it).

# Load data
cd ~/Downloads/combind_data/bpp_data
run ~/Downloads/combind_code/pymol/view_complexes.py
load_complexes AR, 20
load AR/docking/confgen_es4/2HVC_lig-to-1E3G/2HVC_lig-to-1E3G_pv.maegz


# General.
bg_color white
set cartoon_transparency, 0
set cartoon_oval_length, 1
set opaque_background, 0
set cartoon_color, white
set ray_trace_mode, 1
set ray_shadow, 0
set dash_width, 4
set surface_cavity_mode, 2
hide lines
util.cbaw
color cyan, element c and *crystal*
color neon, element c and *prot*
hide labels

# Protein Zoom Out
set_view (\
     0.716844857,    0.440551609,   -0.540400803,\
    -0.695714951,    0.502800286,   -0.512978137,\
     0.045718949,    0.743700802,    0.666936457,\
     0.000494931,   -0.000426412, -169.413162231,\
     6.815871716,   29.233837128,    8.817453384,\
   145.921112061,  193.110198975,  -20.000000000 )

set cartoon_oval_length, 0.5
set cartoon_transparency, 0.1
as cartoon
show surface, br. crystal* expand 3
show sticks, resid 705+752+877 and not name o+c+n and not element h
ray

# Protein Zoom Out Outline

set surface_cavity_mode, 0
set ray_trace_mode, 2 # Remember to undo this after!
ray

# Ligand and protein
set_view (\
     0.716844857,    0.440551609,   -0.540400803,\
    -0.695714951,    0.502800286,   -0.512978137,\
     0.045718949,    0.743700802,    0.666936457,\
     0.000592500,   -0.000375983,  -50.882984161,\
     1.786104202,   30.355800629,    5.576295853,\
    42.240573883,   59.674961090,  -20.000000000 )

show cartoon
hide cartoon, resid 880-900
hide cartoon, resid 707-712
hide cartoon, resid 740-750

show sticks, het
show sticks, resid 705+752+877 and not name o+c+n
hide sticks, element H and not (element N+O extend 1)
hide surface

# Ligand specific

disable *; delete *dist*; enable *1E3G*
dist *1E3G* and name O83, *1E3G* and 752/nh2
dist *1E3G* and name O97, *1E3G* and 705/od1
hide labels
ray

disable; delete *dist*; enable *3B5R*
dist *3B5R* and name N8, *3B5R* and 752/nh2
dist *3B5R* and name O11, *3B5R* and 705/od1
hide labels
ray

disable; delete *dist*; enable *3V49*
dist *3V49* and name N41, *3V49* and 752/nh2
hide labels
ray

disable; delete *dist*; enable *5CJ6*
dist *5CJ6* and name N18, *5CJ6* and 752/nh1
dist *5CJ6* and name O11, *5CJ6* and 705/od1
hide labels
ray

# Glide poses:

color grey90, crystal_2HVC and element c
set stick_transparency, 0.4, crystal_2HVC

delete *dist*
dist 2HVC_lig-to-1E3G_pv.2HVC_lig and name N1, *1E3G* and 705/od1
dist 2HVC_lig-to-1E3G_pv.2HVC_lig and name O21, *1E3G* and 877/og1
hide labels
ray

delete *dist*
dist 2HVC_lig-to-1E3G_pv.2HVC_lig_41 and name O21, *1E3G* and 752/nh2
hide labels
ray

# Ensemble

set stick_radius, 0.2, het
enable 2HVC_lig-to-1E3G_pv.2HVC_lig
enable 2HVC_lig-to-1E3G_pv.2HVC_lig_02
enable 2HVC_lig-to-1E3G_pv.2HVC_lig_03
enable 2HVC_lig-to-1E3G_pv.2HVC_lig_38
enable 2HVC_lig-to-1E3G_pv.2HVC_lig_41
enable 2HVC_lig-to-1E3G_pv.2HVC_lig_56
enable 2HVC_lig-to-1E3G_pv.2HVC_lig_73
enable 2HVC_lig-to-1E3G_pv.2HVC_lig_97
enable 2HVC_lig-to-1E3G_pv.2HVC_lig_99

color magenta, element c and 2HVC_lig-to-1E3G_pv.2HVC_lig
color cyan, element c and 2HVC_lig-to-1E3G_pv.2HVC_lig_02
color orange, element c and 2HVC_lig-to-1E3G_pv.2HVC_lig_03
color salmon, element c and 2HVC_lig-to-1E3G_pv.2HVC_lig_38
color slate, element c and 2HVC_lig-to-1E3G_pv.2HVC_lig_41
color green, element c and 2HVC_lig-to-1E3G_pv.2HVC_lig_56
color yellow, element c and 2HVC_lig-to-1E3G_pv.2HVC_lig_73
color hotpink, element c and 2HVC_lig-to-1E3G_pv.2HVC_lig_97
color lightblue, element c and 2HVC_lig-to-1E3G_pv.2HVC_lig_99


# Surface
set_view (\
     0.716844857,    0.440551609,   -0.540400803,\
    -0.695714951,    0.502800286,   -0.512978137,\
     0.045718949,    0.743700802,    0.666936457,\
     0.000592500,   -0.000375983,  -50.882984161,\
     1.786104202,   30.355800629,    5.576295853,\
    50.740573883,   59.674961090,  -20.000000000 )


set surface_cavity_mode, 2
show surface, het expand 4
hide cartoon
hide sticks
delete *dist*
ray
